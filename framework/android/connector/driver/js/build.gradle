apply plugin: 'com.android.library'

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

// Load gradle properties for JS engine configuration
Properties gradleProperties = new Properties()
def propFile = new File("gradle.properties")
if (propFile.exists()) {
    gradleProperties.load(propFile.newDataInputStream())
}

def cppJsEngine = gradleProperties.getProperty('CPP_JS_ENGINE', 'V8')
def cppHermesComponent = gradleProperties.getProperty('CPP_HERMES_COMPONENT', '')
def cppEnableInspector = gradleProperties.getProperty('CPP_ENABLE_INSPECTOR', 'true')

android {
    compileSdkVersion COMPILE_VERSION as int

    defaultConfig {
        minSdkVersion MIN_VERSION as int
        targetSdkVersion TARGET_VERSION as int
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        
        // Pass JS engine configuration to CMake
        externalNativeBuild {
            cmake {
                arguments "-DJS_ENGINE=${cppJsEngine}"
                if (cppHermesComponent) {
                    arguments "-DHERMES_COMPONENT=${cppHermesComponent}"
                }
                arguments "-DENABLE_INSPECTOR=${cppEnableInspector}"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            externalNativeBuild {
                cmake {
                    arguments "-DJS_ENGINE=${cppJsEngine}"
                    if (cppHermesComponent) {
                        arguments "-DHERMES_COMPONENT=${cppHermesComponent}"
                    }
                    arguments "-DENABLE_INSPECTOR=${cppEnableInspector}"
                }
            }
        }
        debug {
            minifyEnabled false
            externalNativeBuild {
                cmake {
                    arguments "-DJS_ENGINE=${cppJsEngine}"
                    if (cppHermesComponent) {
                        arguments "-DHERMES_COMPONENT=${cppHermesComponent}"
                    }
                    arguments "-DENABLE_INSPECTOR=${cppEnableInspector}"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildFeatures {
        buildConfig = false
    }
    
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.18.1"
        }
    }
}

dependencies {
    compileOnly project(path: ':connector-support')

    implementation 'androidx.annotation:annotation:1.0.0'
}
